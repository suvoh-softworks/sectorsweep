class_name StepHandlerComponent extends Node
@export_category("References")
@export var player : PlayerController
@export_category("Step Settings")
#maximum tolerance for what is considered a step, in terms of angle
@export var surface_threshold : float = 0.3
@export var step_height : float = 0.5

const FEET_ADJUSTED_HEIGHT : float = 0.05
const MIN_MOVEMENT_LENGTH : float = 0.3
const MIN_STEP_HEIGHT : float = 0.1
const MAX_STEP_HEIGHT : float = 0.34
const MIN_DOT_VALUE : float = 0.5

var step_status : String
# Called when the node enters the scene tree for the first time.
func _ready() -> void:
	pass # Replace with function body.

func handle_step_climbing():
	step_status = "No vertical collision detected."
	
	for i in player.get_slide_collision_count():
		var collision = player.get_slide_collision(i)
		var object = collision.get_collider()
		
		if _is_vertical_surface(collision) and object != RigidBody3D: #prevent stepping on physics objects
			var measured_height = _measure_step_height(collision)
			if measured_height >= MIN_STEP_HEIGHT and measured_height < MAX_STEP_HEIGHT and measured_height <= step_height and _is_valid_step_direction(collision):
				player.global_position.y += measured_height
				player.velocity = player.previous_velocity #keep player velocity from previous frame; prevents stopping on stepup
				player.camera.smooth_step(measured_height) #enable camera smoothing on stepup
				step_status = "Step Found, height: " + str(measured_height)
			else:
				step_status = "Step too high: " + str(measured_height)
			break
		else:
			step_status = "No vertical collision detected."

#cast a line from player's head to feet, grabbing the distance between feet and endpoint of raycast
func _measure_step_height(collision : KinematicCollision3D) -> float:
	var space_state = player.get_world_3d().direct_space_state
	var collision_point = collision.get_position()
	
	var player_feet = _get_player_feet_position()
	var player_head_y = player.global_position.y + (player.standing_collision.shape.height / 2)
	
	var ray_start = Vector3(collision_point.x, player_head_y, collision_point.z)
	var ray_end = Vector3(collision_point.x, player_feet.y, collision_point.z)
	
	var query = PhysicsRayQueryParameters3D.create(ray_start, ray_end)
	query.collision_mask = player.collision_mask #use player collision model
	query.exclude = [player.get_rid()] #exclude player model from raycast
	
	var result = space_state.intersect_ray(query)
	if result:
		return result.position.y - player_feet.y
	return 0.0
	
#if vertical surface is found, return true
#otherwise use _check_collision_surface to raycast from feet to top of collided object
func _is_vertical_surface(collision: KinematicCollision3D) -> bool:
	var normal = collision.get_normal()
	
	if abs(normal.y) <= surface_threshold:
		step_status = "CollisionShape: Vertical Collision Found, " + str(normal)
		return true
	return _check_collision_surface(collision)

func _check_collision_surface(collision: KinematicCollision3D) -> bool:
	var space_state = player.get_world_3d().direct_space_state
	var collision_point = collision.get_position()
	
	var player_feet = _get_player_feet_position()
	collision_point.y = player_feet.y
	
	var query = PhysicsRayQueryParameters3D.create(player_feet, collision_point)
	query.collision_mask = player.collision_mask
	query.exclude = [player.get_rid()]
	
	var result = space_state.intersect_ray(query)
	if result and abs(result.normal.y) <= surface_threshold:
		step_status = "Raycast: Vertical Collision Found, " + str(result.normal)
		return true
	step_status = "No vertical collision detected"
	return false
	
func _get_player_feet_position() -> Vector3:
	var feet_pos = player.global_position
	feet_pos.y -= player.standing_collision.shape.height / 2
	feet_pos.y += FEET_ADJUSTED_HEIGHT #small buffer
	return feet_pos
	
func _is_valid_step_direction(collision: KinematicCollision3D) -> bool:
		var collision_normal = collision.get_normal()
		var input_dir = player.get_input_direction()
		var movement_direction = player.transform.basis * Vector3(input_dir.x, 0, input_dir.y)
		if movement_direction.length() > MIN_MOVEMENT_LENGTH:
			movement_direction = movement_direction.normalized()
			var dot_product = movement_direction.dot(-collision_normal)
			return dot_product > MIN_DOT_VALUE
		return false
